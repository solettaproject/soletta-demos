#!/usr/bin/env sol-fbp-runner

# This file is part of the Soletta Project
#
# Copyright (C) 2015 Intel Corporation. All rights reserved.
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

# This is the main flow used in the table of foosball.
# It controls goals per match, reset scores, and communicates with
# scoreboard via OIC. When a team wins it waits for 3 seconds before
# starting next match.

DECLARE=ScoreCounter:fbp:score-counter.fbp
DECLARE=GoalsMax:fbp:goals-max.fbp

RedScore(ScoreCounter:sensor_device=1,sensor_pin=0,led_pin=130)
YellowScore(ScoreCounter:sensor_device=1,sensor_pin=0,led_pin=131)
Max(GoalsMax)

ResetButton(MainResetButton)

ScoreBoard(score-counter/client-score-counter:device_id="580a3d6a9d194a23b90a24573558d2f4")
Reset(converter/boolean-to-empty)
ResetWon(boolean/filter)
ResetTimer(timer:interval=3000)
TimerState(converter/empty-to-boolean:output_value=false)
Disable(constant/boolean:value=false)

RedWon(int/equal)
YellowWon(int/equal)

ResetButton OUT -> PULSE_IF_TRUE Reset
Reset OUT -> RESET RedScore
Reset OUT -> RESET YellowScore

RedScore SCORE -> IN[0] RedWon
Max MAX -> IN[1] RedWon
RedWon OUT -> IN ResetWon

YellowScore SCORE -> IN[0] YellowWon
Max MAX -> IN[1] YellowWon
YellowWon OUT -> IN ResetWon

Disable OUT -> ENABLED ResetTimer
ResetWon TRUE -> ENABLED ResetTimer
ResetTimer OUT -> IN TimerState
TimerState OUT -> ENABLED ResetTimer
TimerState OUT -> PULSE_IF_FALSE Reset

RedScore SCORE -> P1_SCORE ScoreBoard
YellowScore SCORE -> P2_SCORE ScoreBoard
RedWon OUT -> P1_WIN ScoreBoard
YellowWon OUT -> P2_WIN ScoreBoard
